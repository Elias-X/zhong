;;; -*- Mode: TDL; Coding: utf-8 -*-

;;; Zhong [|] Grammar
;;;
;;; created at:
;;;     Tue Jun 30 19:43:27 UTC 2009
;;; based on Matrix customization system version of:
;;;     Tue Jun 23 20:36:41 UTC 2009
;;;
;;; Authors: Nat Hillard, Dan Flickinger, Chunlei Yang
;;;


;; SSH 2014-09-04: Teh style feature for efficient generation
sign :+
 [ STYLE style,
   IDIOMATIC luk ].

;;; STYLE
style := avm & 
 [ WRITTEN luk ].
strict := style &
 [ WRITTEN + ].
robust := style.
spoken := robust & 
 [ WRITTEN - ].
unproductive := robust.
dialect := robust. 

lex-rule :+ 
 [ STYLE #style,
   DTR.STYLE #style ].

basic-unary-phrase :+ 
  [ STYLE #style,
    SYNSEM.PAREN #paren,
    ARGS < [ STYLE #style,
	     SYNSEM.PAREN #paren ] > ].


;; SSH 2014-12-19: BOUND is introduced. Lexical items with [BOUND +]
;; cannot be combined with the other of itself.
synsem :+ 
 [ PUNCTUATED luk,
   INTERJECTED luk,
   PAREN luk,
   ASPECTED luk,
   POSTCOMP luk,
   BARE luk,
   SPART spart,
   BOUND luk ].

spart := avm.
not-crs := spart.
not-ma := spart.
crs := not-ma.
ma := not-crs.
no-spart := spart.
not-crs-not-ma := not-crs & not-ma.


;; SSH 2014-10-24: for chart mapping
;; MATRIX REDEFINITION
;; make STEM of type orthog(raphy) to pass up from/to/form
sign-min := avm &
  [ STEM orthog ].

orthog := cons &
  [ FROM string,
    TO string ].

;; SSH 2015-01-26: compound lexical items
cpd := icons.
noun-noun := cpd.
pred-pred := cpd. 
rslt := pred-pred.
parallel := pred-pred.

card_rel := named_rel.
symbol_rel := named_rel.

card-relation := named-relation & arg1-relation &
  [ PRED card_rel ].

symbol-relation := named-relation & arg1-relation &
  [ PRED symbol_rel ].

; SSH 2014-10-20: type hiearchy for classifers
classifier_x_rel := predsort.
丛_x_rel := classifier_x_rel.
丝_x_rel := classifier_x_rel.
个_x_rel := classifier_x_rel.
串_x_rel := classifier_x_rel.
代_x_rel := classifier_x_rel.
件_x_rel := classifier_x_rel.
份_x_rel := classifier_x_rel.
位_x_rel := classifier_x_rel.
具_x_rel := classifier_x_rel.
册_x_rel := classifier_x_rel.
分_x_rel := classifier_x_rel.
列_x_rel := classifier_x_rel.
则_x_rel := classifier_x_rel.
剂_x_rel := classifier_x_rel.
副_x_rel := classifier_x_rel.
匹_x_rel := classifier_x_rel.
卷_x_rel := classifier_x_rel.
双_x_rel := classifier_x_rel.
发_x_rel := classifier_x_rel.
叠_x_rel := classifier_x_rel.
口_x_rel := classifier_x_rel.
句_x_rel := classifier_x_rel.
只_x_rel := classifier_x_rel.
台_x_rel := classifier_x_rel.
叶_x_rel := classifier_x_rel.
号_x_rel := classifier_x_rel.
名_x_rel := classifier_x_rel.
员_x_rel := classifier_x_rel.
味_x_rel := classifier_x_rel.
回_x_rel := classifier_x_rel.
团_x_rel := classifier_x_rel.
圈_x_rel := classifier_x_rel.
块_x_rel := classifier_x_rel.
坨_x_rel := classifier_x_rel.
堂_x_rel := classifier_x_rel.
堆_x_rel := classifier_x_rel.
堵_x_rel := classifier_x_rel.
声_x_rel := classifier_x_rel.
处_x_rel := classifier_x_rel.
夥_x_rel := classifier_x_rel.
头_x_rel := classifier_x_rel.
套_x_rel := classifier_x_rel.
宗_x_rel := classifier_x_rel.
家_x_rel := classifier_x_rel.
对_x_rel := classifier_x_rel.
封_x_rel := classifier_x_rel.
尊_x_rel := classifier_x_rel.
尾_x_rel := classifier_x_rel.
层_x_rel := classifier_x_rel.
带_x_rel := classifier_x_rel.
帧_x_rel := classifier_x_rel.
席_x_rel := classifier_x_rel.
帮_x_rel := classifier_x_rel.
幅_x_rel := classifier_x_rel.
幢_x_rel := classifier_x_rel.
座_x_rel := classifier_x_rel.
张_x_rel := classifier_x_rel.
截_x_rel := classifier_x_rel.
户_x_rel := classifier_x_rel.
所_x_rel := classifier_x_rel.
扇_x_rel := classifier_x_rel.
扎_x_rel := classifier_x_rel.
打_x_rel := classifier_x_rel.
批_x_rel := classifier_x_rel.
把_x_rel := classifier_x_rel.
招_x_rel := classifier_x_rel.
排_x_rel := classifier_x_rel.
摊_x_rel := classifier_x_rel.
撇_x_rel := classifier_x_rel.
撮_x_rel := classifier_x_rel.
支_x_rel := classifier_x_rel.
方_x_rel := classifier_x_rel.
曲_x_rel := classifier_x_rel.
期_x_rel := classifier_x_rel.
本_x_rel := classifier_x_rel.
朵_x_rel := classifier_x_rel.
杆_x_rel := classifier_x_rel.
束_x_rel := classifier_x_rel.
条_x_rel := classifier_x_rel.
枚_x_rel := classifier_x_rel.
枝_x_rel := classifier_x_rel.
架_x_rel := classifier_x_rel.
栋_x_rel := classifier_x_rel.
栏_x_rel := classifier_x_rel.
株_x_rel := classifier_x_rel.
根_x_rel := classifier_x_rel.
桌_x_rel := classifier_x_rel.
档_x_rel := classifier_x_rel.
棵_x_rel := classifier_x_rel.
款_x_rel := classifier_x_rel.
段_x_rel := classifier_x_rel.
湾_x_rel := classifier_x_rel.
滴_x_rel := classifier_x_rel.
点_x_rel := classifier_x_rel.
片_x_rel := classifier_x_rel.
版_x_rel := classifier_x_rel.
班_x_rel := classifier_x_rel.
畦_x_rel := classifier_x_rel.
盏_x_rel := classifier_x_rel.
票_x_rel := classifier_x_rel.
科_x_rel := classifier_x_rel.
章_x_rel := classifier_x_rel.
笔_x_rel := classifier_x_rel.
管_x_rel := classifier_x_rel.
篇_x_rel := classifier_x_rel.
簇_x_rel := classifier_x_rel.
粒_x_rel := classifier_x_rel.
级_x_rel := classifier_x_rel.
纸_x_rel := classifier_x_rel.
线_x_rel := classifier_x_rel.
组_x_rel := classifier_x_rel.
绺_x_rel := classifier_x_rel.
缕_x_rel := classifier_x_rel.
群_x_rel := classifier_x_rel.
联_x_rel := classifier_x_rel.
股_x_rel := classifier_x_rel.
艘_x_rel := classifier_x_rel.
节_x_rel := classifier_x_rel.
落_x_rel := classifier_x_rel.
行_x_rel := classifier_x_rel.
袭_x_rel := classifier_x_rel.
记_x_rel := classifier_x_rel.
课_x_rel := classifier_x_rel.
起_x_rel := classifier_x_rel.
路_x_rel := classifier_x_rel.
身_x_rel := classifier_x_rel.
轮_x_rel := classifier_x_rel.
轴_x_rel := classifier_x_rel.
辆_x_rel := classifier_x_rel.
辑_x_rel := classifier_x_rel.
进_x_rel := classifier_x_rel.
道_x_rel := classifier_x_rel.
部_x_rel := classifier_x_rel.
重_x_rel := classifier_x_rel.
针_x_rel := classifier_x_rel.
锭_x_rel := classifier_x_rel.
门_x_rel := classifier_x_rel.
间_x_rel := classifier_x_rel.
队_x_rel := classifier_x_rel.
阶_x_rel := classifier_x_rel.
集_x_rel := classifier_x_rel.
面_x_rel := classifier_x_rel.
页_x_rel := classifier_x_rel.
顶_x_rel := classifier_x_rel.
项_x_rel := classifier_x_rel.
颗_x_rel := classifier_x_rel.
题_x_rel := classifier_x_rel.
首_x_rel := classifier_x_rel.


intensifier_x_rel := predsort.
_hen_x_rel := intensifier_x_rel.
_feichang_x_rel := intensifier_x_rel.
redup_x_rel := intensifier_x_rel.

temporal_x_rel := predsort.

#|
lex-sense-default := lex-item &
 [ SYNSEM.LOCAL.CAT.HEAD.KEYS.KEY 个_x_rel ].
lex-sense-animal := lex-sense-default &
 [ SYNSEM.LOCAL.CAT.HEAD.KEYS.KEY 只_x_rel ].
lex-sense-horse := lex-sense-animal &
 [ SYNSEM.LOCAL.CAT.HEAD.KEYS.KEY 匹_x_rel ].
lex-sense-machine := lex-sense-default &
 [ SYNSEM.LOCAL.CAT.HEAD.KEYS.KEY 台_x_rel ].
|#


lzg := avm. 
le := lzg.
zhe := lzg.
guo := lzg.
no-lzg := lzg.
le+zhe := le & zhe.
le+guo := le & guo.
zhe+guo := zhe & guo.
le+zhe+guo := le & zhe & guo.

+vjp :+ 
   [ LZG lzg ].

+vj :+ 
   [ STATIVE luk,
     PRED-KEY predsort ].

char := avm & 
  [ FCHAR string, 
    WCHAR string, 
    LENGTH length ].

length := avm.
one := length.
more-than-one := length.


;; WWJ 2015-04-07: EXISTV for 有; MODIFIABLE used 
;; for A-elements in A-not-A structures
head :+
 [ CHAR char,
   EXISTV bool, 
   MODIFIABLE bool ].

lex-item :+ 
 [ STEM.FIRST #str,
   SYNSEM.LOCAL.CAT.HEAD.CHAR.WCHAR #str ].

marker-or-punct := head & 
 [ MOD <> ].
marker := marker-or-punct.
punct := marker-or-punct.

+np :+ [ PFORM pform ].
pform := avm.
gei-or-xiang := pform.
gei := gei-or-xiang.
xiang := gei-or-xiang.
zai := pform.
you := pform.
dui := pform.

noun :+ 
 [ HON luk,
   GENERIC luk,
   CLASSIFIED luk ].

;; SSH 2014-10-10 nominalizer 的 'de'
nm := noun.

num :+ 
 [ CARDINAL bool ].


;; SSH 2015-04-16: The following are revised. 
prep := adp.
postp := adp.


;; SSH 2014-09-02: Gender is necessary to distinguish he and she. 
;; 她 追赶 了 他: She chased him.
;; In addition, Animacy is also neceesary to disinguish s(he) and it.
;; 她 追赶 了 它: She chasd it.
png :+ 
  [ PERNUM pernum,
    GENDER gender,
    ANIMACY animacy ].

;;; Pernum

pernum := *top*.
sg := pernum.
indet := pernum.
pl := pernum.
1st := pernum.
2nd := pernum.
3rd := pernum.
1sg := 1st & sg.
2sg := 2nd & sg.
3sg := 3rd & sg.
1indet := 1st & indet.
2indet := 2nd & indet.
3indet := 3rd & indet.
1pl := 1st & pl.
1pl_incl := 1pl.
1pl_excl := 1pl.
2pl := 2nd & pl.
3pl := 3rd & pl.

;;; SSH 2014-09-02
;;; Gender 
gender := *top*.
masc := gender.
fem := gender.

;;; SSH 2014-09-02; 2015-04-08 revised 
;;; Animacy
animacy := *top*.
animate := animacy.
human := animate.
non-human := animate.
inanimate := animacy.

;;; Form

form := *top*.
nonfinite := form.
finite := form.

;;; Aspect


;has-aspect := aspect.
perfective := aspect.
durative := aspect.
delimitative := aspect.
;; SSH 2014-09-19: The type hierarchy for aspect should be researched
;; more. We assume here that guo and no-marking imply *imperfective*.
imperfective := aspect.
experiential := imperfective.
;; SSH 2014-09-04; blocking overgeneration
non-aspect := imperfective.



;;; Phrasal types

basic-head-comp-phrase :+ 
  [ SYNSEM.LOCAL.CAT [ MC #mc,
		       VAL.SPEC #spec ],
    HEAD-DTR.SYNSEM [ BOUND na-or--,
		      LOCAL.CAT [ MC #mc,
				  VAL.SPEC #spec ] ],
    NON-HEAD-DTR.SYNSEM.LOCAL.CAT.NONCOMP - ].

basic-head-mod-phrase-simple :+ 
  [ SYNSEM.LOCAL [ CAT.MC #mc,
		   CONT.HOOK.INDEX #ind ],
    HEAD-DTR.SYNSEM.LOCAL.CONT.HOOK.INDEX #ind,
    NON-HEAD-DTR.SYNSEM [ PUNCTUATED na-or--,
			  LOCAL.CAT.MC #mc ] ].



; SSH 2014-08-25 Zhong specific head-adj-phrase This consraint was in
; the previous version of matrix.tdl, but it was removed by Antske
; Fokkens and Joshua Crowgey (2012-26-09).
head-adj-phrase :+ 
  [ SYNSEM.INTERJECTED #interjected,
    HEAD-DTR.SYNSEM [ INTERJECTED #interjected,
		      MODIFIED notmod-or-rmod ] ].

head-adj-int-phrase :+ 
  [ SYNSEM [ L-PERIPH #lperiph,
	     R-PERIPH #rperiph ],
    HEAD-DTR.SYNSEM.R-PERIPH na-or--,
    NON-HEAD-DTR.SYNSEM [ L-PERIPH #lperiph,
			  R-PERIPH #rperiph ] ].

;; WWJ (2015-03-28): NHD's SPART and Phrase's NHD are identi-fied... is this ok?
head-adj-scop-phrase :+
  [ SYNSEM.SPART #spart,
    HEAD-DTR.SYNSEM.ASPECTED +-or--,
    NON-HEAD-DTR.SYNSEM [ BOUND -, 
                          SPART #spart ] ].

;; The head of head-comp-phrase can't be: ['comp']

head-comp-phrase := basic-head-1st-comp-phrase & head-initial &
  [ SYNSEM phr-synsem &
       [ LOCAL.CAT.HEAD +nvjrpdmo ],
    HEAD-DTR.SYNSEM.POSTCOMP na-or--,
    NON-HEAD-DTR.SYNSEM [ R-PERIPH na-or--,
			  INTERJECTED na-or-- ] ].

;; WWJ (2015-03-28) - HEAD-DTR's spart value is passed up to this phrase
head-comp-phrase :+
  [ SYNSEM.SPART #spart,
    HEAD-DTR.SYNSEM.SPART #spart,
    NON-HEAD-DTR.SYNSEM.BOUND - ].


;; SSH 2015-04-16: adp -> prep
head-2nd-comp-phrase := basic-head-2nd-comp-phrase & head-initial &
  [ SYNSEM phr-synsem &
       [ LOCAL.CAT.HEAD verb ],
    HEAD-DTR.SYNSEM.POSTCOMP na-or--,
    NON-HEAD-DTR.SYNSEM [ LOCAL.CAT.HEAD prep,
                          R-PERIPH na-or--,
			  INTERJECTED na-or-- ] ]. 

;; SSH 2015-04-16: adp -> +np (ex. 我 [教室 里] 哭 了)
comp-head-final-phrase := basic-head-1st-comp-phrase & head-final & 
  [ SYNSEM.LOCAL.CAT.HEAD +nvjrpdmo,
    HEAD-DTR.SYNSEM.POSTCOMP +,
    NON-HEAD-DTR.SYNSEM [ R-PERIPH na-or--,
			  LOCAL.CAT.HEAD +np ] ].

;; For now, exclude time nouns as subjects
;; 
;; SSH 2014-09-26: Only phrases can be the subjects (phr-synsem)
subj-head-phrase := decl-head-subj-phrase & head-final &
  [ SYNSEM.LOCAL.CAT.MC #mc,
    SYNSEM.SPART #spart,
    HEAD-DTR.SYNSEM.LOCAL.CAT [ MC #mc,
				VAL [ SPR olist,
				      COMPS < > ],
				POSTHEAD + ],
    HEAD-DTR.SYNSEM.SPART #spart,
    NON-HEAD-DTR.SYNSEM  [ R-PERIPH na-or--,
			   INTERJECTED na-or--,
			   LOCAL [ CAT [ MC +,
					 NONSUBJ -,
					 VAL.SPR <> ],
				   CONT.HOOK.INDEX.SORT nontime-s ],
			   NON-LOCAL [ SLASH 0-dlist & [ LIST < > ],
				       REL 0-dlist ] ] ].

            
;; comp-head-phrase requires things that are [ HEAD comp ].
comp-head-phrase := basic-head-1st-comp-phrase & head-final &
  [ SYNSEM.LOCAL.CAT.HEAD comp,
    NON-HEAD-DTR.SYNSEM [ R-PERIPH na-or--,
			  INTERJECTED na-or-- ] ].



s-coord-phrase :+ 
[ SYNSEM.LOCAL.CAT.MC #mc,
  LCOORD-DTR.SYNSEM.LOCAL.CAT.MC #mc,
  RCOORD-DTR.SYNSEM.LOCAL.CAT.MC #mc ].
 

;;; Coordination Strategy 1

n1-top-coord-rule := basic-np-top-coord-rule & monopoly-top-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "1" ].

n1-mid-coord-rule := basic-np-mid-coord-rule & monopoly-mid-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "1" ].

#|
n1-bottom-coord-rule := infl-bottom-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "1",
    SYNSEM.LOCAL.COORD-REL.PRED "_and_coord_rel",
    DTR.SYNSEM.LOCAL.CAT.HEAD noun ].
|#

bottom-coord-rule := conj-first-bottom-coord-rule &
  [ SYNSEM [ LOCAL [ CAT.HEAD #head,
                     COORD + ],
             NON-LOCAL #nonloc ],
    CONJ-DTR.SYNSEM.LOCAL.CAT.HEAD conj,
    NONCONJ-DTR.SYNSEM [ LOCAL.CAT.HEAD #head,
                         NON-LOCAL #nonloc ] ].


;;; Coordination Strategy 2

vp1-top-coord-rule := basic-vp-top-coord-rule & apoly-top-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "1" ].

#|
vp1-bottom-coord-rule := conj-first-bottom-coord-rule & 
             vp-bottom-coord-phrase &
  [ SYNSEM.LOCAL.COORD-STRAT "1" ].
|#

;;; Coordination Strategy 3

n3-top-coord-rule := basic-n-top-coord-rule & apoly-top-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "3" ].

n3-bottom-coord-rule := unary-bottom-coord-rule & n-bottom-coord-phrase &
  [ SYNSEM.LOCAL [ COORD-STRAT "3",
                   COORD-REL.PRED "_and_coord_rel" ] ].

;;; Coordination Strategy 3

np3-top-coord-rule := basic-np-top-coord-rule & apoly-top-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "3" ].

np3-bottom-coord-rule := unary-bottom-coord-rule & np-bottom-coord-phrase &
  [ SYNSEM.LOCAL [ COORD-STRAT "3",
                   COORD-REL.PRED "_and_coord_rel" ] ].

;;; Coordination Strategy 3

vp3-top-coord-rule := basic-vp-top-coord-rule & apoly-top-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "3" ].

vp3-bottom-coord-rule := unary-bottom-coord-rule & vp-bottom-coord-phrase &
  [ SYNSEM.LOCAL [ COORD-STRAT "3",
                   COORD-REL.PRED "_and_coord_rel" ] ].

;;; Coordination Strategy 3

s1-top-coord-rule := basic-s-top-coord-rule & apoly-top-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "1" ].

#|
s3-bottom-coord-rule := unary-bottom-coord-rule & s-bottom-coord-phrase &
  [ SYNSEM.LOCAL [ COORD-STRAT "1",
                   COORD-REL.PRED "_and_coord_rel" ] ].
|#

;;; Coordination Strategy 4

n4-top-coord-rule := basic-n-top-coord-rule & monopoly-top-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "4" ].

n4-mid-coord-rule := basic-n-mid-coord-rule & monopoly-mid-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "4" ].

n4-bottom-coord-rule := infl-bottom-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "4",
    SYNSEM.LOCAL.COORD-REL.PRED "_and_coord_rel",
    DTR.SYNSEM.LOCAL.CAT.HEAD noun ].

;;; Coordination Strategy 5

n5-top-coord-rule := basic-n-top-coord-rule & monopoly-top-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "5" ].

n5-mid-coord-rule := basic-n-mid-coord-rule & monopoly-mid-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "5" ].

n5-bottom-coord-rule := conj-first-bottom-coord-rule & n-bottom-coord-phrase &
  [ SYNSEM.LOCAL.COORD-STRAT "5" ].

;;; Coordination Strategy 5

np5-top-coord-rule := basic-np-top-coord-rule & monopoly-top-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "5" ].

np5-mid-coord-rule := basic-np-mid-coord-rule & monopoly-mid-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "5" ].

np5-bottom-coord-rule := conj-first-bottom-coord-rule & 
             np-bottom-coord-phrase &
  [ SYNSEM.LOCAL.COORD-STRAT "5" ].

;;; Coordination Strategy 5

vp5-top-coord-rule := basic-vp-top-coord-rule & monopoly-top-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "5" ].

vp5-mid-coord-rule := basic-vp-mid-coord-rule & monopoly-mid-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "5" ].

vp5-bottom-coord-rule := conj-first-bottom-coord-rule & 
             vp-bottom-coord-phrase &
  [ SYNSEM.LOCAL.COORD-STRAT "5" ].

;;; Coordination Strategy 5

s5-top-coord-rule := basic-s-top-coord-rule & monopoly-top-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "5" ].

s5-mid-coord-rule := basic-s-mid-coord-rule & monopoly-mid-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "5" ].

s5-bottom-coord-rule := conj-first-bottom-coord-rule & s-bottom-coord-phrase &
  [ SYNSEM.LOCAL.COORD-STRAT "5" ].

;;; Coordination Strategy 6

s6-top-coord-rule := basic-s-top-coord-rule & monopoly-top-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "6" ].

s6-mid-coord-rule := basic-s-mid-coord-rule & monopoly-mid-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "6" ].

s6-bottom-coord-rule := conj-first-bottom-coord-rule & s-bottom-coord-phrase &
  [ SYNSEM.LOCAL.COORD-STRAT "6" ].

;;; Coordination Strategy 7

s7-top-coord-rule := basic-s-top-coord-rule & monopoly-top-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "7" ].

s7-mid-coord-rule := basic-s-mid-coord-rule & monopoly-mid-coord-rule &
  [ SYNSEM.LOCAL.COORD-STRAT "7" ].

s7-bottom-coord-rule := conj-first-bottom-coord-rule & s-bottom-coord-phrase &
  [ SYNSEM.LOCAL.COORD-STRAT "7" ].

;; We treat question particles as complementizers.
;; Here is the lexical type for complementizers.

adj-head-phrase :+ 
  [ HEAD-DTR.SYNSEM.L-PERIPH na-or-- ].

adj-head-phrase :+ 
  [ SYNSEM.LOCAL.CAT.MC #mc,
    HEAD-DTR.SYNSEM.LOCAL.CAT.MC #mc ].

;; SSH 2015-03-17: *张三 哦 哭 了 。
scopal-mod-phrase :+
  [ SYNSEM.INTERJECTED #interjected,
    HEAD-DTR.SYNSEM.PUNCTUATED na-or--,
    NON-HEAD-DTR.SYNSEM.INTERJECTED #interjected ].

;; SSH 2014-12-19: BOUND for blocking [狗 轻轻 [地 [叫 了]].
adj-head-int-mc := adj-head-int-phrase &
  [ SYNSEM.LOCAL.CAT.NONSUBJ #ns,
    HEAD-DTR.SYNSEM [ SPART no-spart,
		      LOCAL.CAT.NONSUBJ #ns,
                      BOUND na-or-- ],
    NON-HEAD-DTR.SYNSEM [ BOUND na-or--,
			  LOCAL.CAT.NONSUBJ #ns ] ].

adj-head-scop-phrase :+ 
  [ SYNSEM [ L-PERIPH #lperiph,
	     R-PERIPH #rperiph ],
    HEAD-DTR.SYNSEM.BOUND na-or--,
    NON-HEAD-DTR.SYNSEM [ L-PERIPH #lperiph,
			  R-PERIPH #rperiph ] ].

alts :+ 
  [ LE-PFV bool ].


; SSH 2014-08-26: For Topic-Comment Constructions 
; (1) This kind of types should reflect on the order of constituents
; in their names. Thus, I changed the name: head-filler-phrase to
; filler-head-phrase.  (2) I do not understand the purpose and
; function of C-CONs-coord-phraseT. Temporily, I remove it.

#|
head-filler-phrase := basic-head-filler-phrase & head-final & 
  [ SYNSEM.LOCAL.CAT [ VAL #val,
               MC #mc ],
    HEAD-DTR.SYNSEM.LOCAL [ CAT [ HEAD verb,
                  VAL #val & [ SUBJ < >,
                           COMPS < >,
                           SPR < >,
                           SPEC < > ],
                  MC #mc ],
                CONT.HOOK #hook ],
    NON-HEAD-DTR.SYNSEM.NON-LOCAL.QUE 0-dlist,
    C-CONs-coord-phraseT.HOOK #hook ].
|#

filler-head-phrase := basic-head-filler-phrase & head-final & 
		      topic-comment &
  [ SYNSEM [ L-PERIPH +,
	     LOCAL.CAT [ VAL #val,
			 MC #mc ] ],
    HEAD-DTR.SYNSEM [ L-PERIPH -,
		      BOUND na-or--,
		      LOCAL [ CAT [ HEAD verb,
				    VAL #val & [ SUBJ < >,
						 COMPS < >,
						 SPR < >,
						 SPEC < > ],
				    MC #mc ],
			      CONT.HOOK #hook & [ INDEX #clause,
						  CLAUSE-KEY #clause ] ] ],
    NON-HEAD-DTR.SYNSEM [ INTERJECTED na-or--,
			  LOCAL [ CAT.HEAD noun,
				  CONT.HOOK.INDEX #index ],
			  NON-LOCAL.QUE 0-dlist ],
    C-CONT [ HOOK #hook,
	     RELS <! !>, HCONS <! !>, 
	     ICONS <! topic &
		    [ IARG1 #clause,
                      IARG2 #index ] !> ] ].

;; SSH 2014-09-02: added [MODIFIED] for blocking overgeneration
;; SSH 2014-09-02: added [MC +] for blocking overgeneration
extracted-comp-phrase := basic-extracted-comp-phrase & 
  [ SYNSEM.MODIFIED #modified,
    HEAD-DTR.SYNSEM [ MODIFIED #modified,
		      BOUND na-or--,
		      LOCAL [ CAT [ MC +, HEAD verb ],
			      CONT.HOOK #hook ] ],
    C-CONT [ HOOK #hook, RELS <! !>, HCONS <! !>, ICONS <! !> ] ].

;; DPF 2011-06-03 - Given current analysis of relative clauses as containing
;; exactly one extracted element (which is thus relativized), we need subject
;; extr at least for e.g. MRS:53
;; DPF 2011-06-03 - HACK: stamped SPEC < [] > on mother, to avoid immediate
;; filler-head-rule application for embedded clause in "I think dog chase cat".
;; This `works' because the filler-head rule already demands that the hd-dtr
;; have empty SPEC value; note that when an extracted-subj clause is picked
;; up as a complement, the SPEC value is ignored higher up, since the verb
;; selecting the S/NP does not pass up that SPEC value, of course.
;;
extracted-subj-phrase := basic-extracted-subj-phrase & 
  [ SYNSEM [ MODIFIED #modified,
	     LOCAL.CAT [ MC -,
			 VAL.SPEC < [] > ] ],
    HEAD-DTR.SYNSEM [ MODIFIED #modified,
		      BOUND na-or--,
		      LOCAL [ CAT [ HEAD +vj,
                                    VAL.SUBJ < [ LOCAL.CAT.NONSUBJ - ] > ],
			      CONT.HOOK #hook ] ],
    C-CONT [ HOOK #hook, RELS <! !>, HCONS <! !>, ICONS <! !> ] ].

coord-phrase :+ 
  [ SYNSEM.NON-LOCAL.SLASH 0-dlist,
    LCOORD-DTR.SYNSEM.NON-LOCAL.SLASH 0-dlist,
    RCOORD-DTR.SYNSEM.NON-LOCAL.SLASH 0-dlist ].

;; MATRIX REDEFINITION
;; Note: Modified to allow multiple rels 

basic-determiner-lex := norm-hook-lex-item & basic-icons-lex-item & 
  [ SYNSEM [ LOCAL [ CAT [ HEAD det,
               VAL.SPEC.FIRST.LOCAL.CONT.HOOK [ INDEX #ind,
                                  LTOP #larg ] ],
             CONT.HCONS <! qeq &
                 [ HARG #harg,
                   LARG #larg ] !> ],
         LKEYS.KEYREL quant-relation &
           [ ARG0 #ind,
             RSTR #harg ] ] ].

;; SSH 2014-08-26: MATRIX REDEFINITION
infostr-marking-determiner-lex := basic-determiner-lex.


;; MATRIX REDEFINITION
;; Note: for the following, removed QUE 0-dlist -- question words can function 
;; as direct objects
;;
clause := phrasal.

;; MATRIX REDEFINITION
;; Note: Removed [QUE 0-dlist] from HEAD-DTR, to allow in-situ WH
;; 
basic-non-rel-clause := clause & head-compositional & 
  [ SYNSEM.NON-LOCAL.REL 0-dlist,
    HEAD-DTR.SYNSEM [ LOCAL.CONT.HOOK [ INDEX #index,
                                        ICONS-KEY.IARG1 #index,
                                        CLAUSE-KEY #index ],
                      NON-LOCAL.REL 0-dlist ],
    C-CONT [ RELS <! !>,
             HCONS <! !> ] ].

;; MATRIX REDEFINITION
;; Note: We removed the [QUE 0-dlist] constraint from the NON-HEAD-DTR
;; since Mandarin has in-situ WH-phrases.
;;
basic-head-subj-phrase := head-valence-phrase & binary-headed-phrase &
                          head-compositional & 
  [ SYNSEM phr-synsem & 
           [ LOCAL.CAT [ POSTHEAD +,
                         VAL [ SUBJ < >,
                               COMPS #comps,
                               SPR #spr ] ] ],
    HEAD-DTR.SYNSEM [ BOUND na-or--,
		      LOCAL.CAT.VAL [ SUBJ < #synsem >,
				      COMPS #comps,    
				      SPR #spr ] ],
    NON-HEAD-DTR.SYNSEM #synsem & canonical-synsem &
           [ LOCAL.CAT.VAL [ SUBJ olist,
                             COMPS olist,
                             SPR olist ],
             NON-LOCAL [ SLASH 0-dlist & [ LIST < > ],
                         REL 0-dlist ] ],
    C-CONT [ RELS <! !>, HCONS <! !>, ICONS <! !> ] ].

;; Nouns which cannot take specifiers mark their SPR requirement
;; as OPT +.  Making the non-head daughter OPT - in this rule
;; keeps such nouns out.
;; 
;; Rules for building NPs.  Note that the Matrix uses SPR for
;; the specifier of nouns and SUBJ for the subject (specifier) of verbs.

;; MATRIX REDEFINITION
;; Note: We removed the identification of HOOK on mother and NON-HEAD-DTR,
;; since we need two subtypes of head-specifier phrsae, one whose HOOK
;; is identified with the NON-HEAD-DTR, and one where the HOOK is the same
;; as that of the HD-DTR.
;;

;;We changed the head daughter's comps list to the empty list, rather than olist
#|
basic-head-spec-phrase := head-valence-phrase & phrasal &
              binary-headed-phrase & 
  [ INFLECTED infl-satisfied,
    SYNSEM phr-synsem & [ LOCAL.CAT [ VAL [ SUBJ #subj,
                        COMPS #spcomps,
                        SPR #spr,
                        SPEC #spec ],
                      POSTHEAD #ph ],
              MODIFIED #modif ],
    HEAD-DTR [ INFLECTED infl-satisfied,
               SYNSEM [ LOCAL [ CAT [ HEAD #head,
                                      VAL [ SUBJ #subj,
                                            COMPS <> & #comps,
                                            SPR < #synsem & 
                                                  canonical-synsem . #spr >,
                                            SPEC #spec ],
                                      POSTHEAD #ph ],
                                CONT.HOOK #hdhook ],
                        MODIFIED #hmodif ] ],
    NON-HEAD-DTR.SYNSEM #synsem &
          [ LOCAL [ CAT.VAL [ SPEC < [ LOCAL [ CAT [ HEAD #head,
                             VAL.COMPS #comps ],
                           CONT.HOOK #hdhook ],
                       MODIFIED #hmodif ] >,
                  COMPS #spcomps ],
            CONT.HOOK #hook ],
        MODIFIED #modif ],
    C-CONT [ HOOK #hook,  RELS <! !>, HCONS <! !>, ICONS <! !> ] ].

head-spec-phrase := basic-head-spec-phrase & head-final &
  [ NON-HEAD-DTR.SYNSEM [ LOCAL [ CAT [ VAL.SUBJ < >,
                    HEAD det ],
                  CONT.HOOK #hook ] ],
    C-CONT.HOOK #hook ].

;; Note here, the syntactic head is the semantic head
;; note, we deleted NON-HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.SUBJ < > : this is already specified in the heads
;; 
;;; DPF 21-dec-09 - use head-compositional etc instead...

head-spec-h-sem-phrase := basic-head-spec-phrase & head-final &
  [ SYNSEM.LOCAL.CAT.HEAD.MODIFIABLE +,
    HEAD-DTR.SYNSEM.LOCAL [ CAT.HEAD +jrpd & [ MODIFIABLE + ],
                            CONT.HOOK #hook ],
    C-CONT.HOOK #hook ].

; For nominalizer head `de', which must be [HEAD noun], but which is also the
; semantic head.  Cannot easily collapse with either of the other two head-spec
; rules, since the non-head-dtr can be [HEAD det] in the hspec-hs rule where
; the head is a classifier.  FIX someday?
;
head-nom-spec-h-sem-phrase := basic-head-spec-phrase & head-final &
  [ HEAD-DTR.SYNSEM.LOCAL [ CAT.HEAD noun,
                CONT.HOOK #hook ],
    NON-HEAD-DTR.SYNSEM.LOCAL.CAT.HEAD +vjp,
    C-CONT.HOOK #hook ].
|#


;; SSH 2014-08-22 zhong specific basic-head-spec-phrase 
basic-head-spec-phrase := head-valence-phrase & phrasal &
                          binary-headed-phrase & 
  [ STYLE robust,
    INFLECTED infl-satisfied,
    SYNSEM phr-synsem & [ LOCAL [ CAT [ VAL [ SUBJ #subj,
					      SPR #spr,
					      SPEC #spec ],
					POSTHEAD #ph ],
				  CONT.HOOK.CLAUSE-KEY #clause ],
			  MODIFIED #modif ],
    HEAD-DTR [ INFLECTED infl-satisfied,
               SYNSEM [ R-PERIPH na-or--,
			LOCAL [ CAT [ HEAD #head,
				      VAL [ SUBJ #subj,
					    COMPS <> & #comps,
					    SPR < #synsem & 
						  canonical-synsem . #spr >,
					    SPEC #spec ],
                                      POSTHEAD #ph ],
				CONT.HOOK #hdhook & [ CLAUSE-KEY #clause ] ],
			MODIFIED #hmodif ] ],
    NON-HEAD-DTR.SYNSEM #synsem &
     [ R-PERIPH na-or--,
       LOCAL [ CAT.VAL.SPEC < [ LOCAL [ CAT [ HEAD #head,
					      VAL.COMPS #comps ],
					CONT.HOOK #hdhook ],
				MODIFIED #hmodif ] >,
	       CONT.HOOK.CLAUSE-KEY #clause ],
       MODIFIED #modif ],
    C-CONT [ RELS <! !>, HCONS <! !>, ICONS <! !> ] ].


head-spec-phrase := basic-head-spec-phrase & head-final &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < >,
    NON-HEAD-DTR.SYNSEM [ LOCAL [ CAT [ VAL.SUBJ < >,
                    HEAD det ],
                  CONT.HOOK #hook ] ],
    C-CONT.HOOK #hook ].


;; Note here, the syntactic head is the semantic head
;; note, we deleted NON-HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.SUBJ < > : this is already specified in the heads
;; 
;;; DPF 21-dec-09 - use head-compositional etc instead...
;; SSH 2015-04-16 discourse stgatus (definitesness)
; ---------------------------------------
;           | cmn             | yue
; ---------------------------------------
; DEM-CL-N  | uniq-fam-active (definite)
; NUME-CL-N | type-id (indefinite)
; CL-N      | type-id         | active-or-less ((in)definite)
; N         | active-or-less  | type-id 
; ---------------------------------------
head-spec-h-sem-phrase := basic-head-spec-phrase & head-final &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS #spcomps,
    HEAD-DTR.SYNSEM.LOCAL [ CAT.HEAD +jrpd,
			    CONT.HOOK #hook ],
    NON-HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.COMPS #spcomps,
    C-CONT.HOOK #hook ].

; For nominalizer head `de', which must be [HEAD noun], but which is also the
; semantic head.  Cannot easily collapse with either of the other two head-spec
; rules, since the non-head-dtr can be [HEAD det] in the hspec-hs rule where
; the head is a classifier.  FIX someday?
;

head-nom-spec-h-sem-phrase := basic-head-spec-phrase & head-final &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS #spcomps,
    HEAD-DTR.SYNSEM.LOCAL [ CAT.HEAD noun,
                CONT.HOOK #hook ],
    NON-HEAD-DTR.SYNSEM.LOCAL.CAT [ HEAD +vjp,
                    VAL.COMPS #spcomps ],
    C-CONT.HOOK #hook ].

;; Bare NP phrase.  Consider modifying the PRED value of the quantifier 
;; relation introduced to match the semantic effect of bare NPs in your 
;; language.

;; MATRIX REDEFINITION
;; Note: we removed the SPEC <> constraint
;;; DPF - motivate? - shouldn't the head at least propagate SPEC to mother?

basic-bare-np-phrase := head-only &
  [ SYNSEM.LOCAL.CAT.VAL [ SPR < >,
                           SUBJ < >,
                           COMPS < > ],
    HEAD-DTR.SYNSEM.LOCAL [ CAT.VAL [ SPR < [ LOCAL.CAT.HEAD det,
                                              OPT + ] >,
                                      SUBJ < >,
                                      COMPS olist ],
                            CONT.HOOK [ INDEX #index,
                                        LTOP #larg ] ],
    C-CONT [ RELS <! quant-relation &
                   [ LBL #ltop,
                     ARG0 #index,
                     RSTR #harg ] !>,
             HCONS <! qeq & 
                    [ HARG #harg,
                      LARG #larg ] !>,
             ICONS <! !>,
             HOOK [ INDEX #index,
                    LTOP #ltop ] ] ].

bare-np-phrase := basic-bare-np-phrase &
  [ SYNSEM [ BARE +,
	     LOCAL.CAT [ HEAD noun & [ GENERIC - ],,
             NONSUBJ #ns ],
         NON-LOCAL.SLASH 0-dlist ],
    HEAD-DTR.SYNSEM.LOCAL [ CAT.NONSUBJ #ns,
			    CONT.HOOK.INDEX.COG-ST activ-or-less ],
    C-CONT.RELS <! [ PRED "exist_q_rel" ] !> ].

;; CL-phrase => nominal phrase
basic_bare-nominal-phrase := unary-phrase &
  [ SYNSEM.LOCAL [ CAT [ HEAD noun,
                         VAL [ SPR < [ LOCAL.CAT.HEAD det,
                                       OPT + ] >,
                               SUBJ < >,
                               COMPS < > ],
                         POSTHEAD +,
                         NONSUBJ #ns ],
                   COORD -,
                   CONT.HOOK #hook & [ INDEX.SORT nontime-s ] ],
    ARGS < [ SYNSEM.LOCAL.CAT [ VAL [ SUBJ < >,
                                      SPR olist,
                                      COMPS < > ],
                                NONSUBJ #ns ] ] >,
    C-CONT [ RELS <! noun-relation &
                   [ PRED "generic_nom_rel",
                     LBL #ltop,
                     ARG0 #index ] !>,
             HCONS <! !>,
             ICONS <! !>,
             HOOK #hook &
                  [ LTOP #ltop,
                    INDEX #index ] ] ].

bare-nominal-classifier-phrase := basic_bare-nominal-phrase &
  [ ARGS < [ SYNSEM.LOCAL.CAT.HEAD +jd &
             [ MOD < [ LOCAL.CONT.HOOK [ LTOP #ltop,
                             INDEX #index ] ] > ] ] >,
    C-CONT.HOOK [ LTOP #ltop,
          INDEX #index ] ].

bare-nominal-det-phrase := basic_bare-nominal-phrase &
  [ ARGS < [ SYNSEM.LOCAL.CAT [ HEAD det,
                VAL.SPEC < [ LOCAL.CONT.HOOK 
                           [ LTOP #ltop,
                             INDEX #index ] ] > ] ] >,
    C-CONT.HOOK [ LTOP #ltop,
          INDEX #index ] ].


generic-np-phrase := basic-bare-np-phrase &
  [ SYNSEM [ LOCAL.CAT [ HEAD noun & [ GENERIC + ],
			 NONCOMP + ],
	     NON-LOCAL.SLASH 0-dlist ],
    HEAD-DTR.SYNSEM [ BARE -, 
		      LOCAL [ CAT.HEAD.CLASSIFIED na-or--,
			      CONT.HOOK.INDEX.COG-ST type-id ] ],
    C-CONT.RELS <! [ PRED "generic_q_rel" ] !> ].

; Restrict head-dtr to non-empty SPR, to avoid spurious ambiguity.
;
;; SSH 2014-04-16 added [CLASSIFIED na-or--]
meas-np-phrase := head-final & head-valence-phrase & phrasal &
  [ SYNSEM.LOCAL.CAT.VAL [ SUBJ < >,
                           SPR < >,
                           COMPS < > ],
    NON-HEAD-DTR.SYNSEM.LOCAL [ CAT [ HEAD num,
                                      VAL.SPR < > ],
                                CONT.HOOK [ LTOP #ltop,
                                            INDEX #nhind ] ],
    HEAD-DTR.SYNSEM lex-synsem & [ BARE -,
				   LOCAL [ CAT.HEAD noun & [ GENERIC na-or--,
							     CLASSIFIED na-or-- ],
					   CONT.HOOK [ LTOP #ltop,
                                        INDEX #hind ] ] ],
    C-CONT [ HOOK.INDEX #hind,
             RELS <! [ LBL #ltop,
                       PRED "measure-rel",
                       ARG1 #hind,
                       ARG2 #nhind ],
                   quant-relation &
                   [ PRED "exist_q_rel",
                     ARG0 #hind,
               RSTR #harg ] !>,
             HCONS <! qeq & 
                    [ HARG #harg,
                      LARG #ltop ] !>,
             ICONS <! !> ] ].


;; SSH 2014-12-19: 追赶 张三 ！
;; non-asepct & main clause
imperative-phrase := imp-head-opt-subj-phrase &
  [ SYNSEM [ LOCAL.CAT.HEAD verb,
	     NON-LOCAL.SLASH 0-dlist ],
    HEAD-DTR.SYNSEM.LOCAL [ CAT.MC +,
			    CONT.HOOK.INDEX.E.ASPECT non-aspect ] ].

;; DPF 27-jul-11 - Removed HEAD-DTR..MODIFIED notmod to allow "old dog who 
;; can bark" |一 只 会 叫 的 老 狗|
;;
;; SSH 2014-04-16: added [CLASSIFIED na-or--] to prevent [一 只 猫]
;; from being analyzed as a compound
noun-noun-compound-phrase := head-final &
  [ SYNSEM [ LOCAL.CAT #cat,
             NON-LOCAL #nonloc ],
    HEAD-DTR.SYNSEM lex-synsem &
                [ LOCAL [ CAT #cat & 
                              [ HEAD noun & [ CLASSIFIED na-or-- ],
                                VAL [ SPR < synsem >,
                                      COMPS < > ] ],
                          CONT.HOOK [ LTOP #ltop,
                                      INDEX #hind ] ],
                  NON-LOCAL #nonloc ],
    NON-HEAD-DTR.SYNSEM.LOCAL [ CAT [ HEAD noun,
                                      VAL [ SPR < synsem >,
                                            COMPS < > ],
                                      POSTHEAD - ],
                                CONT.HOOK [ LTOP #larg,
                                            INDEX #nhind ] ],
    C-CONT [ HOOK [ LTOP #ltop,
            INDEX #hind ],
	     RELS <! quant-relation & [ PRED "exist_q_rel",
					ARG0 #nhind,
					RSTR #harg ] !>,
             HCONS <! qeq & [ HARG #harg,
                              LARG #larg ] !>,
             ICONS <! noun-noun & [ IARG1 #hind, 
				    IARG2 #nhind ] !> ] ].

;; SSH 2014-12-19: phr-synsem is added.
nom-de-noun-compound-phrase := head-final &
  [ SYNSEM phr-synsem &
           [ LOCAL.CAT #cat,
             NON-LOCAL #nonloc ],
    HEAD-DTR.SYNSEM phr-synsem & [ LOCAL [ CAT #cat & 
					       [ HEAD noun,
						 VAL [ COMPS < > ] ],
					   CONT.HOOK [ LTOP #ltop,
						       INDEX #hind ] ],
				   NON-LOCAL #nonloc ],
    NON-HEAD-DTR.SYNSEM.LOCAL [ CAT [ HEAD noun,
                                      VAL [ SPR < anti-synsem >,
                                            COMPS < > ],
                                      POSTHEAD - ],
                                CONT.HOOK [ LTOP #larg,
                                            INDEX #nhind ] ],
    C-CONT [ HOOK [ LTOP #ltop,
            INDEX #hind ],
	     RELS <! quant-relation & [ PRED "exist_q_rel",
					ARG0 #nhind,
					RSTR #harg ] !>,
             HCONS <! qeq & [ HARG #harg,
                              LARG #larg ] !>,
             ICONS <! noun-noun & [ IARG1 #hind, 
				    IARG2 #nhind ] !> ] ].


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Resultative Compounds 
;;; SSH 2015-04-06
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; @phdthesis{lee:13,
;   title={Event Structure and Grammatical Patterns: Resultative Constructions},
;   author={Lee, Leslie},
;   year={2013},
;   school={University of California, San Diego}
; }
;  Type 4, 6, 13 were not introduced in order to reduce overgeneration (they are rarely used).
;  Type 8 can be handled as head-opt-comp-phrase. 
;  Type 9 is redundant to others except for noun incorporation.
;  Instead, I added two more types in which 给 is used as V2 from the following article.
; @article{li:90,
;   title={{On VV Compounds in Chinese}},
;   author={Li, Yafei},
;   journal={Natural Language \& Linguistic Theory},
;   volume={8},
;   number={2},
;   pages={177--207},
;   year={1990},
;   publisher={Springer}
; }
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

rslt-compound-phrase := head-initial &
  [ SYNSEM [ LOCAL.CAT [ HEAD #head,
			 VAL [ SPR #spr,
			       SPEC #spec ],
			 MC luk, 
			 MKG #mkg,
			 HC-LIGHT #hclight, 
			 POSTHEAD #posthead,
			 NONSUBJ #nonsubj ],
	     NON-LOCAL #nonloc ],
    HEAD-DTR.SYNSEM lex-synsem &
                [ BOUND na-or--,
		  LOCAL [ CAT [ HEAD #head & +vj & [ CHAR.LENGTH one,
						     STATIVE na-or--,
						     AUX - ],				
				VAL [ SPR #spr,
				      SPEC #spec ],
				MC na-or--,
				MKG #mkg,
				HC-LIGHT #hclight, 
				POSTHEAD #posthead,
				NONSUBJ #nonsubj ],
			  CONT [ HOOK [ LTOP #ltop,
					INDEX #arg1 ] ] ],
                  NON-LOCAL #nonloc ],
    NON-HEAD-DTR.SYNSEM lex-synsem &
	        [ LIGHT +,
		  BOUND na-or--,
		  LOCAL [ CAT [ HEAD +vj & [ AUX - ],
				MC na-or-- ],
			  CONT.HOOK.INDEX #arg2 ] ],
    C-CONT [ HOOK [ LTOP #ltop,
		    INDEX #arg1 ],
	     RELS <! !>,
             HCONS <! !>,
	     ICONS <! rslt & [ IARG1 #arg1, 
			       IARG2 #arg2 ] !> ] ].

rslt-x_12-phrase := rslt-compound-phrase & 
  [ SYNSEM.LOCAL.CAT.VAL [ SUBJ < #subj >,
			   COMPS < #comp > ],
    HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.SUBJ < #subj >,
    NON-HEAD-DTR.SYNSEM.LOCAL.CAT.VAL [ SUBJ < #subj >,
					COMPS < #comp > ] ].

rslt-1_1-phrase := rslt-compound-phrase & 
  [ HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.COMPS < >,
    NON-HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.COMPS < > ].

rslt-12_1-phrase := rslt-compound-phrase & 
  [ HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.COMPS < [] >,
    NON-HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.COMPS < > ].

subject-oriented-phrase := head-initial & 
  [ SYNSEM.LOCAL.CAT.VAL.SUBJ < #subj >,
    NON-HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.SUBJ < #subj > ].

object-oriented-phrase := head-initial & 
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < #comp & [ LOCAL.CONT.HOOK.INDEX.PNG.ANIMACY animate ] >,
    NON-HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.SUBJ < #comp > ].

inverse-oriented-phrase := head-initial & 
  [ SYNSEM.LOCAL.CAT.VAL [ SUBJ < #subj & [ LOCAL.CONT.HOOK.INDEX.PNG.ANIMACY inanimate ] >,
			   COMPS < #comp & [ LOCAL.CONT.HOOK.INDEX.PNG.ANIMACY animate ] > ],
    HEAD-DTR.SYNSEM.LOCAL.CAT.VAL [ SUBJ < #comp >,
				    COMPS < #subj > ],
    NON-HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.SUBJ < #comp > ].

;; Type 2 (lee:13)
;;张三 玩 忘 了 责任
rslt-1_12-phrase := rslt-x_12-phrase & subject-oriented-phrase &
  [ HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.COMPS < > ].

;; Type 1 (lee:13)
;;张三 下 赢 棋
rslt-12_12-phrase := rslt-x_12-phrase & subject-oriented-phrase &
  [ HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.COMPS #comps,
    NON-HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.COMPS #comps ].

;; Type 10 (lee:13)
;;张三 跑 累 了
rslt-1_1-sbj-phrase := rslt-1_1-phrase & subject-oriented-phrase &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < >,
    HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.SUBJ < #subj >,
    NON-HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.SUBJ < #subj > ].

;; Type 5 (lee:13)
;;张三 哭 红 了 双眼
rslt-1_1-obj-phrase := rslt-1_1-phrase & object-oriented-phrase &
  [ SYNSEM.LOCAL.CAT.VAL.SUBJ < #subj >,
    HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.SUBJ < #subj > ].

;; Type 11 (lee:13)
;; 双眼 哭 红 了 
rslt-1_1-noshare-phrase := rslt-1_1-phrase & subject-oriented-phrase &
  [ SYNSEM.LOCAL.CAT.VAL [ SUBJ < [ LOCAL.CONT.HOOK.INDEX.PNG.ANIMACY inanimate ] >,
			   COMPS < > ],
    HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.SUBJ < unexpressed & [ OPT + ] > ].

;; Type 7 (lee:13)
;;张三 追 累 了 李四 (Zhangsan chased Lisi and Zhangsan got tired.)
rslt-12_1-sbj-phrase := rslt-12_1-phrase & subject-oriented-phrase &
  [ SYNSEM.LOCAL.CAT.VAL.COMPS < #comp >,
    HEAD-DTR.SYNSEM.LOCAL.CAT.VAL [ SUBJ < #subj >,
				    COMPS < #comp > ],
    NON-HEAD-DTR.SYNSEM.LOCAL.CAT.VAL.SUBJ < #subj > ]. 

;; Type 3 (lee:13)
;;张三 追 累 了 李四 (Zhangsan chased Lisi to the extent of making Lisi tired.)
rslt-12_1-obj-phrase := rslt-12_1-phrase & object-oriented-phrase &
  [ SYNSEM.LOCAL.CAT.VAL [ SUBJ < #subj >,
			   COMPS < #comp > ],
    HEAD-DTR.SYNSEM.LOCAL.CAT.VAL [ SUBJ.FIRST #subj,
				    COMPS < #comp >] ].

;; Type 12 (lee:13)
;;这 种 药 吃 死 你
rslt-12_1-inv-phrase := rslt-12_1-phrase & inverse-oriented-phrase.

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;




;; Make temporal NPs into adposition phrases
np-adp-phrase := unary-phrase &
  [ SYNSEM.LOCAL.CAT [ HEAD adp & 
                [ MOD < [ LOCAL intersective-mod &
                        [ CAT [ HEAD verb,
                            VAL [ COMPS < >,
                              SUBJ < [] > ] ],
                          CONT.HOOK [ LTOP #ltop,
                              INDEX #modind ] ]]> ],
               VAL [ SPR < >,
                 SUBJ < >,
                 COMPS < > ] ],
    ARGS < [ SYNSEM.LOCAL [ CAT [ HEAD noun,
                  VAL.SPR < > ],
                CONT.HOOK.INDEX #nind & [ SORT time-s,
                              SPECI - ] ] ] >,
    C-CONT [ HOOK [ LTOP #ltop,
            INDEX #index,
            XARG #modind ],
         RELS <! arg12-ev-relation &
           [ LBL #ltop,
             PRED temporal_x_rel,
             ARG0 #index,
             ARG1 #modind,
             ARG2 #nind ] !>,
         HCONS <! !>,
         ICONS <! !> ] ].

;; WWJ 2014-04-07: to block adj-not-adj prep-not-prep 
;; SSH 2015-04-08: to block overgeneration, [ASPECTED +-or--] is added.
;; SSH 2015-04-12: verb -> +vp
head-opt-comp-phrase := basic-head-opt-comp-phrase &
  [ SYNSEM phr-synsem, 
    HEAD-DTR.SYNSEM [ ASPECTED +-or--,
		      LOCAL.CAT.HEAD +vp ] ].

; for sentential subjects
sent-subj-phrase := head-valence-phrase & head-only &
  [ SYNSEM [ LOCAL.CAT.VAL [ SUBJ < [ LOCAL [ CAT [ HEAD +vjp,
                            VAL [ SUBJ < >,
                              SPR < >,
                              COMPS < > ] ],
                          CONT.HOOK.LTOP #sltop ] ] >,
                 SPR < >,
                 COMPS < > ],
	     LIGHT - ],
    HEAD-DTR.SYNSEM.LOCAL [ CAT [ VAL [ SUBJ < [ LOCAL.CAT.HEAD noun ] >,
                    COMPS olist ],
                  MC na ],
                CONT.HOOK [ LTOP #ltop,
                    INDEX #index,
                    XARG #xarg ] ],
    C-CONT [ HOOK [ LTOP #ltop,
            INDEX #index ],
         RELS <! !>,
         ICONS <! !>,
         HCONS <! qeq & [ HARG #xarg,
                  LARG #sltop ] !> ] ].


;; MATRIX change: promoted SF feature from event-only to individual,
;; in order to simplify allowing sentential subjects while still excluding
;; imperatives
individual :+
  [ SF iforce ].

;; MATRIX addition: Need to distinguish non-coordinated entities

non-coord-index := event-or-ref-index.
non-coord-event := non-coord-index & event.
non-coord-ref-ind := non-coord-index & ref-ind.

;; Semantic sorts, initially just to distinguish temporal and non-temporal Ns

time-s := semsort.
nontime-s := semsort.

;; MATRIX redefinition: Adopted GTOP of modern Matrix, but also relaxed
;; constraint on XARG to allow for sentential subjects (with handle as XARG).
hook := avm &
  [ GTOP handle,
    LTOP handle,
    INDEX individual,
    XARG semarg,
    ICONS-KEY icons,
    CLAUSE-KEY event ].

basic-head-opt-subj-phrase :+ 
  [ HEAD-DTR.SYNSEM.BOUND na-or-- ].

;; MATRIX redefinition, to allow noun heads to discharge optional complements
;;
basic-head-opt-comp-phrase := head-valence-phrase & head-only &
                              head-compositional &
  [ INFLECTED #infl,
    SYNSEM canonical-synsem &
       [ LOCAL.CAT [ VAL [ SUBJ #subj,
                           COMPS #comps,
                           SPR #spr,
                           SPEC #spec ],
                     MC #mc,
                     POSTHEAD #ph ],
         MODIFIED #mod ],
    HEAD-DTR [ INFLECTED #infl & infl-satisfied,
	       SYNSEM [ BOUND na-or--,
			LOCAL [ CAT [ VAL [ SUBJ #subj,
                                            COMPS < unexpressed &
                                                    [ OPT +,
                                                      OPT-CS #def,
                                                      LOCAL.CONT.HOOK [ INDEX #index & [ COG-ST #def ],
                                                                        ICONS-KEY #ikey,
                                                                        CLAUSE-KEY #ckey ] ] . #comps >,
                                            SPR #spr,
                                            SPEC #spec ],
                                      MC #mc,
                                      POSTHEAD #ph ],
                                CONT.HOOK.INDEX individual ],
                        MODIFIED #mod ] ],
    C-CONT [ RELS <! !>,
             HCONS <! !>,
             ICONS <! #ikey & non-focus & [ IARG1 #ckey,
                                            IARG2 #index ] !> ] ].


; head-gerundive-phrase := head-only &
;   [ SYNSEM canonical-synsem & 
; 	   [ LOCAL.CAT.HEAD.MOD < [] > ],
;     HEAD-DTR.SYNSEM.LOCAL.CAT.HEAD verb ].

; 着了过 aspect markers & sentence final 了 & sentence final question particle 
basic-comp-marker-phrase := head-valence-phrase & head-compositional & basic-binary-headed-phrase &
  [ STYLE #style,
    SYNSEM phr-synsem & [ L-PERIPH #lperiph,
			  R-PERIPH #rperiph,
			  PUNCTUATED #punctuated,
			  PAREN #paren,
			  INTERJECTED #interjected,
			  SPART #spart,
			  LOCAL.CAT [ VAL [ SUBJ #subj,
					    COMPS #comps,
					    SPR #spr,
					    SPEC #spec ],
				      POSTHEAD #ph ] ],
    HEAD-DTR.SYNSEM #synsem & [ L-PERIPH #lperiph,
				R-PERIPH #rperiph,
				PUNCTUATED na-or--,
				POSTCOMP na-or--,
				INTERJECTED #interjected,
				LOCAL [ CAT [ VAL [ SUBJ #subj,
						    COMPS #comps,
						    SPR #spr,
						    SPEC #spec ],
					      POSTHEAD #ph ] ] ],
    NON-HEAD-DTR [ STYLE #style,
		   SYNSEM [ L-PERIPH #lperiph,
			    R-PERIPH #rperiph,
			    PUNCTUATED #punctuated,
			    PAREN #paren,
			    SPART #spart,
			    LOCAL.CAT [ HEAD marker-or-punct,
					VAL.COMPS < #synsem > ] ] ],
    C-CONT [ RELS <! !>,
             HCONS <! !>,
	     ICONS <! !> ] ].

comp-marker-phrase := basic-comp-marker-phrase & marker-final-phrase & head-initial & 
  [ NON-HEAD-DTR.SYNSEM.LOCAL.CAT.POSTHEAD na-or-+ ].
marker-comp-phrase := basic-comp-marker-phrase & marker-initial-phrase & head-final & 
  [ NON-HEAD-DTR.SYNSEM.LOCAL.CAT [ HEAD punct,
                                    POSTHEAD na-or-- ] ].	
 

;; DPF 2014-08-01 - Added unary and binary bridging rules to accommodate robust
;; full-forest treebanking.  The strategy is to pump each normal edge in the
;; chart to a "bridge head" edge via the unary rule, and then combine two
;; adjacent such bridge-head edges using the binary rule.  This strategy,
;; proposed by Woodley Packard, results in a mere doubling of the number of
;; edges in each cell of the chart, rather than the square of the number of
;; normal edges that resulted from trying just a binary bridging rule that
;; accepted either bridge or normal edges.
;;

bridge-head := head-min &
  [ BRIDGED luk ].

bridge-binary-phrase := basic-binary-phrase &
  [ SYNSEM [ LOCAL [ CAT [ HEAD bridge-head & [ BRIDGED + ],
                           VAL [ SUBJ < >,
                                 SPR < >,
                                 COMPS < > ] ] ],
             NON-LOCAL non-local-none ],
    C-CONT [ HOOK [ LTOP #lbl, INDEX #arg0 ],
	     RELS <! [ LBL #lbl,
		       PRED "bridge_x_rel",
		       ARG0 #arg0,
		       ARG1 #ind,
		       ARG2 #rind ] !>,
             HCONS <! !>,
	     ICONS <! !> ],
    ARGS < [ SYNSEM.LOCAL [ CAT.HEAD bridge-head & [ BRIDGED - ],
			    CONT.HOOK.INDEX #ind ] ],
           [ SYNSEM.LOCAL [ CAT.HEAD bridge-head,
			    CONT.HOOK.INDEX #rind ] ] > ].

bridge-unary-phrase := basic-unary-phrase &
  [ SYNSEM [ LOCAL [ CAT [ HEAD bridge-head,
                           VAL [ SUBJ < >,
                                 SPR < >,
                                 COMPS < > ] ] ],
             NON-LOCAL non-local-none ],
    C-CONT [ HOOK #hook,
	     RELS <! !>,
             HCONS <! !>,
	     ICONS <! !> ],
    ARGS < [ SYNSEM.LOCAL [ CAT.HEAD head,
			    CONT.HOOK #hook ] ] > ].

