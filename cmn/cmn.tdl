;;; -*- Mode: TDL; Coding: utf-8 -*-
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Grammar of Mandarin Chinese - Core
;;;
;;; created by Sanghoun Song (2014-08-28)
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


;================================================================================
; This file (cmn.tdl) exists to store syntactical information that pertains only
; Mandarin Chinese (i.e. where it can differ from Cantonese and other varieties)
; -- expected to be true for both Traditional (zht) and Simplified (zhs).
;
; Currently, this includes mainly information on ASPECT and CLASSIFIERS.
;================================================================================


;================================================================================
; ASPECT
;================================================================================

+vjp :+ [ LZG lzg ].

lzg := avm. 

le := lzg
"""
LZG (LE-ZHE-GUO) restriction: can, at least, take LE.
""".

zhe := lzg
"""
LZG (LE-ZHE-GUO) restriction: can, at least, take ZHE.
""".

guo := lzg
"""
LZG (LE-ZHE-GUO) restriction: can, at least, take GUO.
""".

no-lzg := lzg
"""
LZG (LE-ZHE-GUO) restriction: cannot take any aspectual particle (e.g. 是).
""".

le+zhe := le & zhe
"""
LZG (LE-ZHE-GUO) restriction: can, at least, take LE and ZHE.
""".

le+guo := le & guo
"""
LZG (LE-ZHE-GUO) restriction: can, at least, take LE and GUO.
""".

zhe+guo := zhe & guo
"""
LZG (LE-ZHE-GUO) restriction: can, at least, take ZHE and GUO.
""".

le+zhe+guo := le & zhe & guo
"""
LZG (LE-ZHE-GUO) restriction: can take LE, ZHE and GUO.
""".


basic-pred-adj-lex :+ [ SYNSEM.LOCAL.CAT.HEAD.LZG no-lzg ]
"""
This essentially blocks predicative adjectives from taking any kind of aspect.
This is being done here (and not in zhong-lextypes.tdl) since this is seen as 
a Mandarin only restriction.
[FIXME] Need to ask Joanna whether or not this restriction would also happen
in Cantonese and, if so, more this higher. It is confusing to be split.
""". 


;; SSH 2015-04-28: verbal particles

pfv-marker := aspect-markers &
"""
This is used for 了, to mark perfective aspect.
Aspect markers such as 了 take a complement  (currently +vj)

"""
 [ SYNSEM.LOCAL.CAT.VAL [ COMPS < [ BOUND na-or--,
				    LOCAL [CONT.HOOK.INDEX.E.ASPECT perfective,
                                           CAT.HEAD +vj] ] > ] ].

exp-marker := aspect-markers & 
 [ SYNSEM.LOCAL.CAT.VAL [ COMPS < [ LOCAL.CONT.HOOK.INDEX.E.ASPECT experiential ] > ] ].
  
;; SSH 2014-09-02: removing irules.
dur-marker := aspect-markers & 
 [ SYNSEM.LOCAL.CAT.VAL [ COMPS < [ BOUND na-or--,
				    LOCAL.CONT.HOOK.INDEX.E.ASPECT durative ] > ] ].


;; SSH 2014-09-11: [SPEC <>] is added to block 了 不.
;; WWJ 2015-03-17: Added LOCAL...SF prop to block use with A-not-A
;; ZZF 2016-12-2: removed LOCAL.CONT.HOOK.INDEX.SF prop, as they can go with questions as well
;; SSH 2015-04-28: sentence-final particles
;; LMC changed to le (lexical entry type)

;================================================================================
; LE TYPES
;
;================================================================================
crs_le := spart-lex-item & nonque-item &
"""
CRS stands for 'Currently Relevant State', as proposed by Li & Thompson (1981).
It refers to the uses of 了 (LE) and it broadly encompasses multiple uses:
(1) change of state, (2) corrects a wrong assumption, (3) reporting progress
(4) reports what will happen next, (5) marks the speaker's total contribution
to the conversation at that point.

[FIXME]However 'crs' is being used to refer to sentence final particles in 
general this should be changed eventually. E.g. 吧_crs, 啊_crs, 啦_crs, etc.
crs_le is possibly not the right type for all these. 
We should probably create more subtypes of spart-lex-item, and make sure
we can encode the possible combinations of sent-final-particles.
"""
  [ STYLE robust,
    SYNSEM [ R-PERIPH +,
	     SPART crs,
	     LOCAL.CAT [ VAL [ COMPS < [ SPART not-crs-not-ma,
					 LOCAL.CAT [ HEAD +vj,
						     VAL [ SUBJ < >,
							   COMPS < > ],
						     MC #mc ],
					 NON-LOCAL.SLASH 0-dlist ] >,
			       SPEC < >],
			 MC #mc ] ] ].


;; SSH 2015-04-16: In Mandarin Chinese, classifiers must co-occur with
;; numerals. (ex. *(一) 只 猫 走 了).
classifier-lex :+ no-spr-classifier-rule-dtr & 
  [ SYNSEM.LOCAL.CAT.VAL.SPR < [ OPT - ] > ].

no-spr-classifier-lex-rule := const-val-change-only-lex-rule & 
 [ SYNSEM.LOCAL.CAT [ NONSUBJ +,
		      HEAD.MOD < [ LOCAL [ CAT.HEAD.GENERIC -,
					   CONT.HOOK.INDEX.COG-ST type-id ] ] >,
		      VAL [ SUBJ <>,
			    COMPS <>,
			    SPEC <>,
			    SPR <> ] ],
   DTR no-spr-classifier-rule-dtr,
   C-CONT [ RELS <! !>, HCONS <! !>, ICONS <! !> ] ].

no-spr-classifier-rule-dtr := word-or-lexrule.


;; SSH 2015-05-07: Mandarin Specific Predsort.

_de_dem_q_rel := dem_rel.
_zhi_dem_q_rel := dem_rel.
_de_p_assoc_rel := related_p_rel.

_de_nom_rel := nm_rel.
_zhi_nom_rel := nm_rel.

_bu_x_rel := neg_x_rel.
_buqi_x_rel := neg_x_rel.
_bu_polar_x_rel := neg_x_rel.
_mei_x_rel := neg_x_rel.
_mei_polar_x_rel := neg_x_rel.

_hen_x_rel := intensifier_x_rel.
_feichang_x_rel := intensifier_x_rel.

