;;; -*- Mode: TDL; Coding: utf-8 -*-

;===============================================================================
; LMC 2020-02-13
;
;
; These are types (both rules and lexical) for the educational zhong.
;
; it has the types that will be instantiated by mal-lex and mal-rules.
; if it gets bigger in the future, we might want to split it into 
; mal-lextype and mal-ruletypes
;
;===============================================================================




;===============================================================================
; REDEFINITION OF EXISTING TYPES: 
;===============================================================================

pred-adj-lex :+  [SYNSEM.LOCAL.CAT.VAL.SUBJ.FIRST.LOCAL.CAT.HEAD noun]
"""
this is the lexical type for predicative adjectives, and we are constraining it to require only noun subjects. This might be enough for LVL1, must check.
"""
.



;===============================================================================
; NEW TYPES: 
;===============================================================================

qpart-mal-lex-item := spart-lex-item & nonque-item &
"""
This is a copy of qpart-lex-item (sentence final particle), that changed NON-LOCAL.QUE to 1-dlist (instead of 0-dlist). This change is requirering that exacly 1 question word be included in the sentence before this particle can attach. This will enable the use of mal-吗-le even after the sentence is already a question.
"""
  [ STYLE robust,
    SYNSEM [ SPART ma,
             PUNCTUATED -,
	     LOCAL [ CAT.VAL.COMPS < [ SPART not-ma,
                                       LOCAL.CAT.VAL.SPR <>,
                                       NON-LOCAL.QUE 1-dlist ] >, 
		     CONT.HOOK.INDEX.SF ques ] ] ].

mal_x_-_qpart_le := qpart-mal-lex-item.


;;;main-verb-lex &
raising_adj_cop :=  no-rels-hcons-lex-item & basic-two-arg-no-hcons & basic-icons-lex-item
		   & aspect-rule-dtr & verb-no-spr-item & nonque-item & 
[ SYNSEM [ LOCAL [ CAT [HEAD verb & [COP +,
				     EMPTY -,
				     AUX -],
			VAL [ SUBJ < #subj >,
			      COMPS < #comp >] ],
		   CONT.HOOK.CLAUSE-KEY #clause ],
	   NON-LOCAL.SLASH 0-dlist,
	   LKEYS.KEYREL [ ARG1 #ltop,
			  ARG2 #ind2 ] ],
  ARG-ST < [ LOCAL [ CAT [ HEAD noun,
			   VAL [ SUBJ < >,
				 COMPS < > ] ],
		     CONT.HOOK [LTOP #ltop,
				CLAUSE-KEY #clause] ] ],
	   #comp &
	   [ LOCAL [ CAT [ VAL [ SUBJ < #subj >,
				 SPR olist,
				 COMPS < > ],
			   HEAD adj ],
		     CONT.HOOK [ INDEX #ind2,
				 CLAUSE-KEY #clause ] ] ] > ].

mal_v_adj_cop_le := raising_adj_cop
"""
This is used for adjectival sentences to allow a copula.
<mex>我 是 漂亮 。
<nex>我 是 学生 。
"""
.


;===============================================================================
; NEW RULES: 
;===============================================================================

mal-bare-vp-phrase := unary-phrase & verb-no-spr-item &
"""
To pump NPs to VPs.
And [ EMPTY + ] on parent node to signal this VP is not a headed phrase (i.e. there no verb). This might be useful, for example to prevent verb-derivational rules from applied to this case (where there is no vebr). e.g. reduplication of verb, V-Not-V.

Inheriting from verb-no-spr-item restricts the verb phrase being created form taking degree modifiers.


olist = optional or filled
"""
[ SYNSEM [ LOCAL [ CAT [ HEAD verb & [ EMPTY +,
				       COP +],
			 VAL [ SUBJ < [LOCAL [ CAT [ HEAD noun,
						   VAL [ SUBJ < >,
							 SPR < >,
							 COMPS < > ] ],
					       CONT.HOOK.INDEX #arg1 ] ] >,
			       COMPS < > ],
			 POSTHEAD +,
			 NONSUBJ #ns ],
		   COORD -,
		   CONT.HOOK #hook ],
	   NON-LOCAL #nonlocal ],
  ARGS < [ SYNSEM [ LOCAL [ CAT [ HEAD noun & [EMPTY -],
				  VAL [ SUBJ < >,
					SPR < >,
					COMPS < > ],
				  NONSUBJ #ns ],
			    CONT.HOOK.INDEX #arg2 ],
		    NON-LOCAL #nonlocal ] ] >,
  C-CONT [ RELS <! arg12-ev-relation &
		 [ PRED "是_v_cop_rel",
		   LBL #ltop,
		   ARG0 #index,
		   ARG1 #arg1,
		   ARG2 #arg2 ] !>,
	   HCONS <! !>,
	   ICONS <! !>,
	   HOOK #hook &
		[ LTOP #ltop,
		  INDEX #index ] ] ].


